title: Capital Strike
subtitle: Germany's industries are idling...
view-if: (capital_strike_progress >= 10 or budget <= -5) and spd_in_government
new-page: true
max-visits: 1
on-arrival: unemployed += 5; workers_spd*= 0.7; new_middle_spd -= 15; old_middle_spd -= 15; unemployed_spd*=0.7; rural_spd -= 15; pro_republic -= 15; capital_strike_seen = 1; z_relation -= 10; dvp_relation -= 10; ddp_relation -= 8; lvp_relation -= 8; workers_nsdap += 8; unemployed_nsdap += 10; new_middle_nsdap += 15; old_middle_nsdap += 15; rural_nsdap += 15; dvp_right += 5; ddp_right += 3; ddp_cohesion -= 1; lvp_right += 4; coalition_dissent += 1; hindenburg_angry += 100; economic_growth -= 4;
tags: event

= Capital Strike

Our policies have greatly angered the capitalists. Investment has fallen, and factories are idling, preventing the workers from fulfilling their jobs. Meanwhile, the bourgeois press is a constant chorus of venomous invective directed at us and our economic policies.

What can we do?

- @seize
- @capital_controls
- @propaganda
- @printer
- @nothing

@seize
title: Empower workers to seize the factories!
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : The bulk of the party does not support this radical action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?] [? if rb_militancy < 0.25 : If we increased the strength of our paramilitary forces, perhaps we could do this. ?] [? if works_councils < 1 : We need the support of the works councils first. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 1; coup_progress += 6; unemployed -= 2; unemployed -= 1 if works_councils >= 3; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; dvp_relation -= 4; ddp_relation -= 4; lvp_relation -= 4; workers_nsdap -= 8*(1-dissent); unemployed_nsdap -= 8*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; factory_takeovers += 1; dvp_right += 5; ddp_right += 3; lvp_right += 4; economic_growth += 2;

If the capitalists are going to attack us, then we must hit them back. We have empowered the workers to seize and take full control of the factories that the capitalists have idled. The <span style="color: #7A3C00;">fascists</span>, of course, call us thieves and looters.


@capital_controls
title: Enact capital controls to lessen the impact.
choose-if: judicial_reform >= 2
unavailable-subtitle: The judiciary would never allow this.
on-arrival: unemployed -= 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); economic_growth += 2;

Capital controls might blunt some of the worst economic components of the capital strike.

@printer
title: Print money to give to the workers to lessen the impact.
subtitle: What do you mean we already tried this in 1923?
choose-if: state_reichsbank and in_spd_majority
unavailable-subtitle: [? if not state_reichsbank: Reichsbank ?] [? if not in_spd_majority: Our coalition partners ?] would never allow this.
on-arrival: unemployed -= 2; inflation += 10; inflation *= 1.5 if inflation > 0; workers_spd += 6; unemployed_spd += 6; dvp_relation -= 3; ddp_relation -= 3; lvp_relation -= 3; dvp_right += 3; ddp_right += 2; lvp_right += 2; economic_growth += 2; new_middle_spd -= 7; old_middle_spd -= 7;

We have printed more money to allocate to alleviate the worst effects of the crisis. However, inflation has begun to skyrocket.

@propaganda
title: Launch a propaganda campaign blaming the capitalists for the crisis.
subtitle: -1 resources
choose-if: (radio >= 2 or commercialized_media >= 3) and resources >= 1
unavailable-subtitle: [? if radio < 2 and commercialized_media < 3 : We have not built up our media enough. ?] [? if resources < 1: We do not have enough resources. ?]
on-arrival: workers_spd += 7*(1-dissent); new_middle_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); pro_republic += 5*(1-dissent); resources -= 1

We launch a counter-propaganda campaign to blame the economic crisis on the capitalists, using all of our media channels to do so. Because of our existing media framework, this is moderately successful in directing popular criticisms towards the capitalists and away from us.


@nothing
title: There is nothing we can do.

There is nothing we can do that is within constitutional and legal bounds.
